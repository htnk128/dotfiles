# fzf
function fzf-select-history() {
    local tac
    if which tac > /dev/null; then
        tac="tac"
    else
        tac="tail -r"
    fi
    BUFFER=$(history -n 1 | \
        eval $tac | \
        fzf --query "$LBUFFER")
    CURSOR=$#BUFFER
    zle clear-screen
}
zle -N fzf-select-history
bindkey '^r' fzf-select-history

function fzf-select-branch() {
    local target="$(git for-each-ref --format='%(refname:short) | %(committerdate:relative) | %(committername) | %(subject)' --sort=-committerdate refs/heads refs/remotes \
        | column -t -s '|' \
        | grep -ve "^heads" \
        | fzf \
        | awk '{print $1}')"
    if [ -z "$target" ]; then
        zle clear-screen
        return
    fi

    target_branch=${target#remotes\/}
    if [ -z "`git branch | grep ${target_branch}`" ]; then
        BUFFER="git checkout -b $target_branch $target"
    else
        BUFFER="git checkout $target_branch"
    fi
    zle accept-line
}
zle -N fzf-select-branch
bindkey '^g' fzf-select-branch

function fzf-src () {
    local selected_dir=$(ghq list --full-path | fzf --query "$LBUFFER")
    if [ -n "$selected_dir" ]; then
        BUFFER="cd ${selected_dir}"
        zle accept-line
    fi
    zle clear-screen
}
zle -N fzf-src
bindkey '^f' fzf-src

function fzf-ssh () {
    local server=$(cat ~/.ssh/config \
        | grep "Host " \
        | column -t \
        | sed -e 's/Host  //g' \
        | fzf --query "$LBUFFER")
    if [ -z "$server" ]; then
        zle clear-screen
    else
        BUFFER="ssh $server"
        zle accept-line
    fi
}
zle -N fzf-ssh
bindkey '^J' fzf-ssh
